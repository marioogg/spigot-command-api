plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.13.1'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.3'
}

group 'me.marioogg'
version '1.1'

repositories {
    maven { url 'https://maven.marioogg.dev/repository/public/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifact(tasks.shadowJar)
        }
    }

    repositories {
        maven {
            def isSnapshot = version.toString().endsWith("SNAPSHOT")
            def releasesRepoUrl = "https://maven.marioogg.dev/repository/releases"
            def snapshotsRepoUrl = "https://maven.marioogg.dev/repository/snapshots"

            url = uri(isSnapshot ? snapshotsRepoUrl : releasesRepoUrl)

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

dependencies {
    compileOnly('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    compileOnly('dev.folia:folia-api:1.20.1-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    compileOnly 'net.md-5:bungeecord-api:1.20-R0.2-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    archiveClassifier.set('')
}
